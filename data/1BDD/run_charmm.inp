* Run Segment Through CHARMM
*

! read topology and parameter files

read rtf card name $CHARMM_HOME/toppar/top_all22_prot.rtf
read param card name $CHARMM_HOME/toppar/par_all22_prot.prm

! Read sequence from the PDB coordinate file
open unit 1 card read name 1bdd_non_charmm.pdb
read sequ pdb unit 1

! now generate the PSF and also the IC table (SETU keyword)
generate setu a-pro first NTER last CTER

rewind unit 1

! set bomlev to -1 to avois sying on lack of hydrogen coordinates
bomlev -1
read coor pdb unit 1
! them put bomlev back up to 0
bomlev 0

close unit 1

! prints out number of atoms that still have undefined coordinates.
define test select segid a-pro .and. ( .not. hydrogen ) .and. ( .not. init ) show end

ic para
ic fill preserve
ic build
!hbuild sele all end
hbuild

! write out the protein structure file (psf) and
! the coordinate file in pdb and crd format.

write coor pdb name 1bdd_pdb2pqr_charmm.pdb
* PDB
*

write psf CARD OLDPSF name 1bdd_pdb2pqr_charmm.psf
* PSF
*

write coor card name 1bdd_pnon_charmm.crd
* Coords
*

stop
* Run Segment Through CHARMM
*

! read topology and parameter files

read rtf card name ../params/pdbamino_new.rtf
read param card name ../params/parm_new.prm

! Read sequence from the PDB coordinate file
open unit 1 card read name ala_dip.pdb
read sequ pdb unit 1

! now generate the PSF and also the IC table (SETU keyword)
generate setu a-pro first none last none

rewind unit 1

! set bomlev to -1 to avois sying on lack of hydrogen coordinates
bomlev -1
read coor pdb unit 1
! them put bomlev back up to 0
bomlev 0

close unit 1

! prints out number of atoms that still have undefined coordinates.
define test select segid a-pro .and. ( .not. hydrogen ) .and. ( .not. init ) show end

ic para
ic fill preserve
ic build
hbuild sele all end

! write out the protein structure file (psf) and
! the coordinate file in pdb and crd format.

write coor pdb name ala_dip_charmm.pdb
* PDB
*

write psf CARD OLDPSF name ala_dip_charmm.psf
* PSF
*

write coor card name ala_dip_charmm.crd
* Coords
*

stop